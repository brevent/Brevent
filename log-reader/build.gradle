apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.targetSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
    }

    buildTypes {
        release {
            ndk {
                setAbiFilters rootProject.abiFilters
            }
        }
        debug {
            ndk {
                setAbiFilters rootProject.abiFilters
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

}

task deleteLogStub(type: Delete) {
    delete fileTree('build') {
        include '**/liblog.so'
    }
}

task deleteNativeBuild(type: Delete) {
    delete file('.externalNativeBuild')
}

tasks.whenTaskAdded { task ->
    if (task.name == 'externalNativeBuildRelease' || task.name == 'externalNativeBuildDebug') {
        task.finalizedBy deleteLogStub
    }
    if (task.name == 'clean') {
        task.finalizedBy deleteNativeBuild
    }
}
