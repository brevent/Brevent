buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
        maven {
            url 'https://plugins.gradle.org/m2'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        // https://maven.fabric.io/public/io/fabric/tools/gradle/maven-metadata.xml
        classpath 'io.fabric.tools:gradle:1.23.0'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.5'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://dl.google.com/dl/android/maven2'
        }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getBreventVersion = { ->
    def tag = 'git describe --tags --always --dirty'.execute().text.trim()
    if (tag.startsWith('v')) {
        tag = tag.substring(1)
    }
    return tag
}

def isIde = { ->
    def ide = false
    def taskRequests = project.gradle.startParameter.taskRequests
    if (taskRequests?.empty) {
        ide = true
    } else {
        taskRequests[0].args.any {
            if (it.endsWith('generateDebugSources') || it.endsWith('sonarqube')) {
                ide = true
                return true
            }
        }
        return ide
    }
}

project.ext.abiFilters = ['armeabi', 'arm64-v8a', 'x86', 'x86_64']

project.ext.supportLibraryVersion = '26.0.2'

project.ext.buildToolsVersion = '26.0.1'

project.ext.minSdkVersion = 21

project.ext.targetSdkVersion = 26

project.ext.versionCode = 95

if (rootProject.file('.git').exists()) {
    project.ext.simpleVersionName = getBreventVersion()
} else {
    project.ext.simpleVersionName = rootProject.file('brevent-server.txt').text.trim().substring(1)
}

project.ext.versionName = project.ext.simpleVersionName

if (rootProject.file('ant.properties').exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(rootProject.file('ant.properties')))
    if (properties['build.codename'] != null) {
        project.ext.versionName = properties['build.codename'] + '(' + project.ext.simpleVersionName + ')'
    }
}

project.ext.isIde = isIde()
project.ext.hiddenApi = files(project(':hidden-api').file('build/intermediates/bundles/default/classes.jar'))
