buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.google.com/dl/android/maven2'
        }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        if (file('brevent/fabric.properties').exists()) {
            // https://maven.fabric.io/public/io/fabric/tools/gradle/maven-metadata.xml
            classpath 'io.fabric.tools:gradle:1.25.1'
        }
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://dl.google.com/dl/android/maven2'
        }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getBreventVersion = { ->
    def tag = 'git describe --tags --always --dirty'.execute().text.trim()
    if (tag.startsWith('v')) {
        tag = tag.substring(1)
    }
    return tag
}

def isIde = { ->
    def ide = false
    def taskRequests = project.gradle.startParameter.taskRequests
    if (taskRequests?.empty) {
        ide = true
    } else {
        taskRequests[0].args.any {
            if (it.endsWith('generateDebugSources') || it.endsWith('assembleDebug')) {
                ide = true
                return true
            }
        }
        return ide
    }
}

project.ext.javaVersion = JavaVersion.VERSION_1_8

project.ext.abiFilters = ['armeabi', 'arm64-v8a', 'x86', 'x86_64']

project.ext.supportLibraryVersion = '27.0.2'

project.ext.buildToolsVersion = '27.0.3'

project.ext.minSdkVersion = 21

project.ext.targetSdkVersion = 27

project.ext.versionCode = 143

if (rootProject.file('.git').exists()) {
    project.ext.simpleVersionName = getBreventVersion()
} else {
    project.ext.simpleVersionName = rootProject.file('brevent-server.txt').text.trim().substring(1)
}

project.ext.versionName = project.ext.simpleVersionName

if (rootProject.file('ant.properties').exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(rootProject.file('ant.properties')))
    if (properties['build.codename'] != null) {
        project.ext.versionName = properties['build.codename'] + '(' + project.ext.simpleVersionName + ')'
    }
}

project.ext.isIde = isIde()
project.ext.hiddenApi = files(project(':hidden-api').file('build/intermediates/bundles/release/classes.jar'))
