buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        // https://maven.fabric.io/public/io/fabric/tools/gradle/maven-metadata.xml
        classpath 'io.fabric.tools:gradle:1.23.0'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://dl.google.com/dl/android/maven2"
        }
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getBreventVersion = { ->
    def tag = 'git describe --tags --always'.execute().text.trim()
    if (tag.startsWith('v')) {
        tag = tag.substring(1)
    }
    if ('git diff --no-ext-diff --quiet HEAD'.execute().waitFor() == 0) {
        return tag
    } else {
        return tag + '-dirty'
    }
}

project.ext.abiFilters = ['armeabi', 'arm64-v8a', 'x86', 'x86_64']

project.ext.supportLibraryVersion = '26.0.0'

project.ext.buildToolsVersion = '26.0.1'

project.ext.minSdkVersion = 21

project.ext.targetSdkVersion = 26

project.ext.versionCode = 71

if (rootProject.file('.git').exists()) {
    project.ext.simpleVersionName = getBreventVersion()
} else {
    project.ext.simpleVersionName = 'brevent'
}

project.ext.versionName = project.ext.simpleVersionName

if (rootProject.file('ant.properties').exists()) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(rootProject.file('ant.properties')))
    if (properties['build.codename'] != null) {
        project.ext.versionName = properties['build.codename'] + "(" + project.ext.simpleVersionName + ")"
    }
}

project.ext.hiddenApi = files(project(':hidden-api').file('build/intermediates/bundles/default/classes.jar'))
